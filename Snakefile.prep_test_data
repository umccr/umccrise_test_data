import os
import shutil
import csv
from ngs_utils.bcbio import BcbioProject
from os.path import join, abspath, relpath, basename, isfile
from glob import glob
from ngs_utils.file_utils import splitext_plus
from ngs_utils.reference_data import get_cancer_genes_ensg
from hpc_utils.hpc import get_ref_file


shell.executable(os.environ.get('SHELL', 'bash'))
shell.prefix('')  # Fixes Snakemake on Spartan


genome = 'GRCh37'
TRUTH_REGIONS = get_ref_file(genome, ['truth_sets', 'giab', 'bed'])
PON_DIR = get_ref_file('GRCh37', key='panel_of_normals_dir')
CANCER_GENES_ENSG = get_cancer_genes_ensg()

# CWL: '/g/data/gx8/projects/Grimmond_Avner/2018-07-31T0309_Avner_WGS-merged/cromwell/final'
BCBIO_DIR = config.get('final', '/data/cephfs/punim0010/data/Results/Patients/CUP_SC932/final')
run = BcbioProject(BCBIO_DIR)
batch_by_name = {b.tumor.name: b for b in run.batch_by_name.values() if not b.is_germline()}


SORT_SEED = "--random-source <(echo aaaaaaaaaaaaaaa)"


Out_PON_PATH = 'data/panel_of_normals'
bcbio_copy_path = config.get('out', 'data/bcbio_test_project')
bcbio_copy_final_dir = join(bcbio_copy_path, 'final')
bcbio_copy_date = join(bcbio_copy_final_dir, basename(run.date_dir))


mq_list_files = join(run.date_dir, 'multiqc', 'list_files_final.txt')
# if the file exists, we populate QC (because CWL output currently missing this file as longs as all qc)


def extract_genes_from_vcf(inp='{input}', out='{output}'):
    return f'bcftools view -h {inp} > {out} && ' \
           f'bcftools view -H {inp} | zgrep -f {CANCER_GENES_ENSG} >> {out}'

def vcf_to_bed():
    return "bcftools view -H {input} | py -x \"'\\t'.join([x.split()[0], str(int(x.split()[1])-1), x.split()[1]])\"" \
           " > {output}"

def downsample_vcf(num, inp='{input}', out='{output}'):
    return 'bcftools view -h ' + inp + ' > ' + out + ' && ' \
           'bcftools view -H ' + inp + ' -f.,PASS | sort -R ' + SORT_SEED + ' | head -n' + str(num) + '' \
          ' | sort -k1,1 -k2,2n >> ' + out



rule all:
    input:
        expand('work_snake/{batch}_tumor.bam{ext}', batch=batch_by_name.keys(), ext=['', '.bai']),
        expand('work_snake/{batch}_normal.bam{ext}', batch=batch_by_name.keys(), ext=['', '.bai']),
        'work_snake/roi.bed',
        expand(join(bcbio_copy_path, '.populated_batch_{batch}.done'), batch=batch_by_name.keys()),
        join(bcbio_copy_path, '.populated_qc.done') if isfile(mq_list_files) else [],
        join(bcbio_copy_date, '.rsynced.done'),
        giab_regions = 'data/genomes/Hsapiens/GRCh37/validation/giab-NA12878/truth_regions.bed',
        pon_snps_vcf = join(Out_PON_PATH, 'panel_of_normals.snps.vcf.gz'),
        pon_indels_vcf = join(Out_PON_PATH, 'panel_of_normals.indels.vcf.gz')


######################################
#### SOMATIC ####
rule downsample_somatic:
    input:
        lambda wc: join(run.date_dir, f'{batch_by_name[wc.batch].name}-ensemble-annotated.vcf.gz')
    output:
        'work_snake/somatic/{batch}-ensemble.vcf'
    shell:
        extract_genes_from_vcf()

rule somatic_roi:
    input:
        rules.downsample_somatic.output[0]
    output:
        'work_snake/somatic/{batch}-ensemble.bed'
    shell:
        vcf_to_bed()

######################################
#### GERMLINE ####
rule downsample_germline:
    input:
        lambda wc: join(run.date_dir, f'{batch_by_name[wc.batch].normal.name}-germline-ensemble-annotated.vcf.gz')
    output:
        'work_snake/germline/{batch}-germline-ensemble.vcf'
    shell:
        extract_genes_from_vcf()

rule downsample_germline_random100:
    input:
        rules.downsample_germline.output[0]
    output:
        'work_snake/germline/{batch}-germline-ensemble-100.vcf'
    shell:
        downsample_vcf(100)

rule germline_roi:
    input:
        rules.downsample_germline_random100.output[0]
    output:
        'work_snake/germline/{batch}-germline-ensemble.bed'
    shell:
        vcf_to_bed()

######################################
#### CNVKIT ####
rule downsample_cnvkit:
    input:
        lambda wc: join(batch_by_name[wc.batch].tumor.dirpath, f'{batch_by_name[wc.batch].name}-sv-prioritize-cnvkit.vcf.gz')
    output:
        'work_snake/sv/{batch}-cnvkit.vcf'
    shell:
        downsample_vcf(10)

rule cnvkit_roi:
    input:
        rules.downsample_cnvkit.output[0]
    output:
        'work_snake/sv/{batch}-cnvkit.bed'
    shell:
        vcf_to_bed()

rule downsample_cnvkit_cns:
    input:
        lambda wc: join(batch_by_name[wc.batch].tumor.dirpath, f'{batch_by_name[wc.batch].name}-cnvkit-call.cns')
    output:
        'work_snake/sv/{batch}-cnvkit-call.cns'
    shell:
        'head -n1 {input} > {output} && '
        'cat {input} | sort -R ' + SORT_SEED + ' | head -n10 | sort -k1,1 -k2,2n >> {output}'

######################################
#### MANTA ####
rule downsample_manta:
    input:
        lambda wc: join(batch_by_name[wc.batch].tumor.dirpath, f'{batch_by_name[wc.batch].name}-sv-prioritize-manta.vcf.gz')
    output:
        'work_snake/sv/{batch}-manta.vcf'
    shell:
        'bcftools view -f.,PASS,REJECT,Intergenic,MissingAnn {input} -o {output}'

rule manta_roi:
    input:
        rules.downsample_manta.output[0]
    output:
        'work_snake/sv/{batch}-manta.bed'
    shell:
        vcf_to_bed()

rule downsample_sv_prioritize:
    input:
        tsv = lambda wc: join(batch_by_name[wc.batch].tumor.dirpath, f'{batch_by_name[wc.batch].name}-sv-prioritize.tsv'),
        manta_vcf = rules.downsample_manta.output[0]
    output:
        'work_snake/sv/{batch}-sv-prioritize-tsv'
    shell:
        'head -n1 {input.tsv} > {output} && '
        'grep -f <(cut -f2 {input.manta_vcf}) {input.tsv} >> {output}'

######################################
#### BAMS ####
rule batch_roi:
    input:
        rules.somatic_roi.output[0],
        rules.germline_roi.output[0],
        rules.cnvkit_roi.output[0]
    output:
        'work_snake/{batch}-roi.bed'
    shell:
        'cat {input} | bedtools sort -i stdin | bedtools merge -i stdin > {output}'

# adding few chrY-unique locations to make goleft happy
# rule sex_bed:
#     output:
#         'work_snake/sex.bed'
#     run:
#         sex_bed = 'Y\t2655029\t2655030\n' + \
#                   'Y\t2713681\t2713682\n' + \
#                   'Y\t6939595\t6939596\n'
#         with open(output[0], 'w') as out:
#             out.write(sex_bed)

rule project_roi:
    input:
        expand(rules.batch_roi.output[0], batch=batch_by_name.keys())
    output:
        'work_snake/roi.bed'
    shell:
        'cat {input} | bedtools sort -i stdin | bedtools merge -i stdin > {output}'

rule subset_bam:
    input:
        bam = lambda wc: getattr(batch_by_name[wc.batch], wc.phenotype).bam,
        roi_bed = rules.project_roi.output[0]
    output:
        'work_snake/{batch}_{phenotype}.bam'
    shell:
        'samtools view {input.bam} -L {input.roi_bed} -O bam | samtools sort -o {output} /dev/stdin'

rule index_bam:
    input:
        'work_snake/{batch}_{phenotype}.bam'
    output:
        'work_snake/{batch}_{phenotype}.bam.bai'
    shell:
        'samtools index {input}'

# rule bcbio_csv:
#     input:
#         expand('work_snake/{batch}_tumor.bam{ext}', batch=batch_by_name.keys(), ext=['', '.bai']),
#         expand('work_snake/{batch}_normal.bam{ext}', batch=batch_by_name.keys(), ext=['', '.bai']),
#         roi_bed = rules.project_roi.output[0]
#     output:
#         'work_snake/bcbio.csv'
#     run:
#         sample_dicts = []
#         samples_added = set()
#         for s in run.samples:
#             if s.phenotype == 'germline':
#                 pass
#             elif s.name in samples_added:
#                 pass
#             else:
#                 samples_added.add(s.name)
#                 sample_dicts.append({
#                     'samplename': s.batch.tumor.name + '_' + s.phenotype,
#                     'description': s.name,
#                     'batch': s.batch.name,
#                     'phenotype': s.phenotype,
#                     'variant_regions': abspath(input.roi_bed),
#                 })
#         with open(output[0], 'w') as out:
#             writer = csv.DictWriter(out, fieldnames=sample_dicts[0].keys())
#             writer.writeheader()
#             for sd in sample_dicts:
#                 writer.writerow(sd)
#
# rule make_bcbio_yaml:
#     input:
#         bams = expand('work_snake/{batch}_{phenotype}.bam', batch=batch_by_name.keys(), phenotype=['tumor', 'normal']),
#         csv = rules.bcbio_csv.output[0],
#         template = 'bcbio_template.yaml'
#     output:
#         'bcbio/config/bcbio.csv'
#     shell:
#         '/home/vlad/bcbio/anaconda/bin/bcbio_nextgen.py -w template {input.template} {input.csv} {input.bams}'
#
# rule run_bcbio:
#     input:
#         rules.make_bcbio_yaml.output[0]
#     output:
#         'bcbio/final'
#     shell:
#         'cp run.sh bcbio/work; cd bcbio/work; sbatch run.sh'


rule rsync_project:
    input:
        run.date_dir,
        run.config_dir
    output:
        marker = join(bcbio_copy_date, '.rsynced.done')
    params:
        project_copy = bcbio_copy_path
    run:
        shell(
            f'rsync -tavz {run.config_dir} {params.project_copy}'
            f' && rsync -tavz'
            f' --exclude ".snakemake/"'
            f' --exclude "umccrised/"'
            f' --exclude "test/"'
            f' --include "*/"'
            f' --include "data_versions.csv"'
            f' --include "programs.txt"'
            f' --include "multiqc_report.html"'
            f' --exclude "*"'
            f' {run.final_dir} {params.project_copy}'
        )
        shell('touch {output.marker}')


if isfile(mq_list_files):
    qc_files = [line.strip() for line in open(mq_list_files) if line.strip()]
    inp_qc_files = [join(run.final_dir, qc_f) for qc_f in qc_files]
    out_qc_files = [join(bcbio_copy_final_dir, qc_f) for qc_f in qc_files]

    rule populate_qc:
        input:
            mq_list_files = join(run.date_dir, 'multiqc', 'list_files_final.txt'),
            mq_yaml = join(run.date_dir, 'multiqc', 'multiqc_config.yaml'),
            inp_qc_files = inp_qc_files
        output:
            mq_list_files = join(bcbio_copy_date, 'multiqc', 'list_files_final.txt'),
            mq_yaml= join(bcbio_copy_date, 'multiqc', 'multiqc_config.yaml'),
            out_qc_files = out_qc_files,
            marker = bcbio_copy_path + '/.populated_qc.done'
        run:
            shell(f'cp {input.mq_list_files} {output.mq_list_files}')
            shell(f'cp {input.mq_yaml} {output.mq_yaml}')
            for i_f, o_f in zip(input.inp_qc_files, output.out_qc_files):
                shell(f'cp {i_f} {o_f}')

            shell('touch {output.marker}')


rule populate_batch:
    input:
        somatic_vcf = rules.downsample_somatic.output[0],
        germline_vcf = rules.downsample_germline_random100.output[0],
        cnvkit_vcf =  rules.downsample_cnvkit.output[0],
        cnvkit_cns =  rules.downsample_cnvkit_cns.output[0],
        sv_prio = rules.downsample_sv_prioritize.output[0],
        manta_vcf = rules.downsample_manta.output[0],
        tumor_bam = 'work_snake/{batch}_tumor.bam',
        normal_bam = 'work_snake/{batch}_normal.bam',
        tumor_bai = 'work_snake/{batch}_tumor.bam.bai',
        normal_bai = 'work_snake/{batch}_normal.bam.bai'
    output:
        marker = bcbio_copy_path + '/.populated_batch_{batch}.done'
    run:
        batch = batch_by_name[wildcards.batch]

        somatic_vcf = join(bcbio_copy_path, 'final', basename(run.date_dir), f'{batch.name}-ensemble-annotated.vcf.gz')
        shell(f'bgzip -c {input.somatic_vcf} > {somatic_vcf} && tabix {somatic_vcf}')

        germline_vcf = join(bcbio_copy_path, 'final', basename(run.date_dir), f'{batch.normal.name}-germline-ensemble-annotated.vcf.gz')
        shell(f'bgzip -c {input.germline_vcf} > {germline_vcf} && tabix {germline_vcf}')

        cnvkit_vcf = join(bcbio_copy_path, 'final', basename(batch.tumor.dirpath), f'{batch.name}-sv-prioritize-cnvkit.vcf.gz')
        shell(f'bgzip -c {input.cnvkit_vcf} > {cnvkit_vcf} && tabix {cnvkit_vcf}')

        cnvkit_cns = join(bcbio_copy_path, 'final', basename(batch.tumor.dirpath), f'{batch.name}-cnvkit-call.cns')
        shell(f'cp {input.cnvkit_cns} {cnvkit_cns}')

        manta_vcf = join(bcbio_copy_path, 'final', basename(batch.tumor.dirpath), f'{batch.name}-sv-prioritize-manta.vcf.gz')
        shell(f'bgzip -c {input.manta_vcf} > {manta_vcf} && tabix {manta_vcf}')

        sv_prio = join(bcbio_copy_path, 'final', basename(batch.tumor.dirpath), f'{batch.name}-sv-prioritize.tsv')
        shell(f'cp {input.sv_prio} {sv_prio}')

        tumor_bam_name = basename(batch.tumor.bam)
        normal_bam_name = basename(batch.normal.bam)
        tumor_dir = join(bcbio_copy_path, 'final', basename(batch.tumor.dirpath))
        normal_dir = join(bcbio_copy_path, 'final', basename(batch.normal.dirpath))
        shell(f'cp {input.tumor_bam} {tumor_dir}/{tumor_bam_name}')
        shell(f'cp {input.tumor_bai} {tumor_dir}/{tumor_bam_name}.bai')
        shell(f'cp {input.normal_bam} {normal_dir}/{normal_bam_name}')
        shell(f'cp {input.normal_bai} {normal_dir}/{normal_bam_name}.bai')

        shell('touch {output.marker}')


######################################
#### Reference data ####
rule prep_ref_data:
    input:
        giab_regions = TRUTH_REGIONS,
        roi_bed = rules.project_roi.output[0]
    output:
        giab_regions = 'data/genomes/Hsapiens/GRCh37/validation/giab-NA12878/truth_regions.bed'
    shell:
        'bedtools intersect -a {input.giab_regions} -b {input.roi_bed} > {output.giab_regions}'


######################################
#### Panel of normals ####
rule project_somatic_roi:
    input:
        expand(rules.somatic_roi.output[0], batch=batch_by_name.keys())
    output:
        'work_snake/somatic_roi.bed'
    shell:
        'cat {input} | bedtools sort -i stdin | bedtools merge -i stdin > {output}'

rule prep_pon_snps_vcf:
    input:
        pon_vcf = join(PON_DIR, 'panel_of_normals.snps.vcf.gz'),
        somatic_roi = rules.project_somatic_roi.output[0]
    output:
        vcf = join(Out_PON_PATH, 'panel_of_normals.snps.vcf.gz')
    shell:
        'bedtools intersect -header -a {input.pon_vcf} -b {input.somatic_roi} | ' \
        'bgzip -c > {output.vcf} && tabix {output.vcf}'

rule prep_pon_indels_vcf:
    input:
        pon_vcf = join(PON_DIR, 'panel_of_normals.indels.vcf.gz'),
        somatic_roi = rules.project_somatic_roi.output[0]
    output:
        vcf = join(Out_PON_PATH, 'panel_of_normals.indels.vcf.gz')
    shell:
        'bedtools intersect -header -a {input.pon_vcf} -b {input.somatic_roi} | ' \
        'bgzip -c > {output.vcf} && tabix {output.vcf}'


# rule prep_pon:
#     input:
#         pon_vcf = rules.prep_pon_vcf.output[0],
#         pon_snakefile = join(Spa_PON_DIR, 'Snakefile.prep_normals')
#     output:
#         pon_vcf = join(Out_PON_PATH, 'panel_of_normals.vcf.gz')
#     params:
#         pon = Out_PON_PATH,
#         vcf_path = lambda wc, input: relpath(input.pon_vcf, Out_PON_PATH)
#     shell:
#         'snakemake -p -s {input.pon_snakefile} --directory {params.pon} --config normal="{params.sname}:{params.vcf_path}"'


# rule prep_pon:
#     input:
#         pon_vcf = rules.prep_pon_vcf.output[0],
#         pon_snakefile = join(Spa_PON_DIR, 'Snakefile.prep_normals')
#     output:
#         pon_vcf = Out_PON_PATH + '/vcfs/' + PON_SAMPLE_NAME + '.vcf.gz',
#         pon_lua = Out_PON_PATH + '/code.lua',
#         pon_toml = Out_PON_PATH + '/annotate_normals_vcfanno.toml'
#     params:
#         sname = PON_SAMPLE_NAME,
#         pon = Out_PON_PATH,
#         vcf_path = lambda wc, input: relpath(input.pon_vcf, Out_PON_PATH)
#     shell:
#         'snakemake -p -s {input.pon_snakefile} --directory {params.pon} --config normal="{params.sname}:{params.vcf_path}"'














